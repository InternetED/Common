if (isApplication.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'

apply plugin: 'maven-publish'

android {
    compileSdk 31

    defaultConfig {
        if (isApplication.toBoolean()) {
            applicationId "com.interneted.androidcommon"
        }

        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        consumerProguardFiles 'proguard-rules.pro' // Library provider main library proguard-rules

        buildConfigField "boolean", "IS_APPLICATION", isApplication
    }


    sourceSets {
        main {
            // 獨立除錯與整合除錯時使用不同的 AndroidManifest.xml 檔案
            if (isApplication.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {


    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.activity:activity-ktx:1.4.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // AppLifecycle (https://github.com/hufeiyang/Android-AppLifecycleMgr)
    api 'com.github.hufeiyang.Android-AppLifecycleMgr:applifecycle-api:1.0.4'
    kapt 'com.github.hufeiyang.Android-AppLifecycleMgr:applifecycle-compiler:1.0.4'
// kapt 'com.github.hufeiyang.Android-AppLifecycleMgr:applifecycle-compiler:1.0.4' => 業務組件需依賴此

    // LifeData
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    api "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"

    // Kotlin Android Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"


    // Glide
    api "com.github.bumptech.glide:glide:4.12.0"
    kapt "com.github.bumptech.glide:compiler:4.12.0"
    // kapt("androidx.room:room-compiler:$room_version") => 業務組件需依賴此


    // Retrofit
    api "com.squareup.retrofit2:retrofit:2.9.0"
    api "com.squareup.retrofit2:converter-gson:2.9.0"
    api "com.squareup.retrofit2:converter-scalars:2.9.0"

    // Okhttp
    api platform("com.squareup.okhttp3:okhttp-bom:4.9.3")

    api "com.squareup.okhttp3:okhttp"
    api "com.squareup.okhttp3:logging-interceptor"

    // PersistentCookieJar
    api 'com.github.franmontiel:PersistentCookieJar:v1.0.1'


    // Koin
    api "io.insert-koin:koin-android:3.1.4"

    // Gson
    api "com.google.code.gson:gson:2.8.8"

    // Import the BoM for the Firebase platform
    api platform('com.google.firebase:firebase-bom:29.0.1')

    api 'com.google.firebase:firebase-analytics-ktx'
    api 'com.google.firebase:firebase-crashlytics-ktx'


    // Room
    api "androidx.room:room-runtime:2.4.1"
    api "androidx.room:room-ktx:2.4.1"
    // kapt("androidx.room:room-compiler:$room_version") => 業務組件需依賴此
}


// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {

        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.InternetED'
                artifactId = 'androidcommon'
                version = '1.0.0'
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.github.InternetED'
                artifactId = 'androidcommon-debug'
                version = '1.0.0'
            }
        }
    }
}